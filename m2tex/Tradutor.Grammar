#ifndef GRAMATICA_GRAMMAR
#define GRAMATICA_GRAMMAR

#pragma -idchars "-"
#pragma -w 200

compounds
	|-
	\top
	\lf{
	}{
	\fCenter
	\exists
	\neg
	\dland
	\forall
	\rl{
	\AxiomInf$
	\UnaryInf$
	\BinaryInf$
end compounds

tokens 
	charlit "" 
end tokens

define program
	[entrada]|[saida]
end define

define entrada
	[repeat termo]
end define

define saida
	[repeat traducao]
end define

define traducao
	[opt number][repeat qqtoken][NL]
end define

define termo
	'[ [regra] [informacao] '] [NL]
end define

define regra
	[ordem] 'by [tipo_de_regra] 'is
end define

define informacao
	[list conteudo] '|- [list conteudo]
end define

define conteudo
	'< [formula] '| [formula] '| [formula] '>
end define

define ordem 
	[number] 'from [number]
end define

define tipo_de_regra
	[plica][id]
end define

define plica
	'
end define

define formula
	[predicado]
end define

define predicado
	[predicado_vazio] |
	[expressao] |
	[expressao] [virgula] [predicado] |
	[expressao] [operador_conjuncao] [predicado]
end define

define expressao
	[parte_atomica] |
	[operador_negacao] [expressao] |
	[operador_quantitativo] '( [formula] ')
end define

define predicado_vazio
	'nil
end define

define parte_atomica
	[c_true] |
	[id]
end define

define c_true
	'CTRUE
end define

define operador_negacao
	'~
end define

define operador_conjuncao
	'&
end define

define virgula
	',
end define

define operador_quantitativo
	'EXIST |
	'ALL
end define

define aux_comando
	[comando][number]
end define

define comando
	'\AxiomInf$ |
	'\UnaryInf$ |
	'\BinaryInf$
end define

#endif